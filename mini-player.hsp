onexit *owari
onerror *er
onclick gosub *seek
    screen 0,260,110
    title "mini-player 1.0"
    
    color $03,$a1,$fc
    boxf

    objsize 76, 20
    pos 5,10:button "OPEN",*psh_open
    pos 81,10:button "PLAY",*psh_play
    pos 157,10:button "STOP",*psh_stop
    objsize 20,20
    pos 233,10:button gosub "0",*flg_repeat

    mci "set HSP time format milliseconds"

    color 0,0,0
    boxf 10,40,250,100

    font "MS UI Gothic",11,1

    stop
    
    *psh_open
    mci "status HSP mode"
    logmes refstr
    if refstr!="" : mci "close HSP"
    dialog "mp3",16,"MP3"
    if stat=0:stop
    file=refstr

    mci "open \""+refstr+"\" alias HSP"
	filename=getpath(file,8)
    exist file
    sdim id3_data,0x80
    bload file, id3_data,,strsize-0x80

    color
    boxf 10,40,250,80
	mci "status HSP length"
	mci_length=stat
	bai=1.0*mci_length/24
   if wpeek(id3_data,0)!0x4154 : goto *psh_play

    color $03,$66,$fc

    getstr id3_title,id3_data,0x03
    pos 20,45
    mes id3_title

    getstr id3_artist,id3_data,0x21
    pos 20,65
    mes id3_artist


*psh_play
	mci "status HSP mode"
	if refstr="stopped" | refstr="paused" : mci "play HSP" :objprm 1,"PAUSE": else : mci "stop HSP" : objprm 1,"PLAY"
    repeat
    	gosub *draw
    	pos 20,45
    	mes filename
        await 100
        if mci_length <= posi&&repeat_flg=0 : goto *psh_stop : break
        if mci_length <= posi&&repeat_flg=1 : mci "seek HSP to 0" : mci "play HSP"
    loop

*psh_stop
    mci "stop HSP"
    mci "seek HSP to 0"
    objprm 1,"PLAY"
    gosub *draw
    stop

*owari
    mci "close HSP"
    end

*er
    logmes "error "+err
    if err=19 : pos 20,45 : mes "再生するファイルを選択してください"
    stop

*draw
    pget 0,0
    boxf
    color 0,0,0
    boxf 10,40,250,100
    color 255,255,255
    boxf 10,91,250,94
    color $03,$66,$fc
    mci "status HSP position"
    posi=stat
    boxf (1.0*posi)/(mci_length)*230+10,88,(1.0*posi)/(mci_length)*230+15,98
    title filename+" - "+strf("%02d",posi/1000/60)+":"+strf("%02d",posi/1000\60)
    pos 115,80
    mes strf("%02d",posi/1000/60)+":"+strf("%02d",posi/1000\60)
return

*flg_repeat
    if repeat_flg=0:repeat_flg=1:objprm 3,"1":else:repeat_flg=0:objprm 3,"0"
return

*seek
    repeat
        getkey key,1
        mci "status HSP position"
        posi=stat
        if key = 1{
            if mousex>=(1.0*posi)/(mci_length)*230+10&mousey>=88&mousex<=(1.0*posi)/(mci_length)*230+15&mousey<=98&cnt=0{
                a=1
            }if a=1{    
                seek2=1*(((1.0*mousex-10)/230)*mci_length)
            }
            }else{
            mci "play HSP from "+seek2
            a=0
            break
        }
        wait 10
    loop
return